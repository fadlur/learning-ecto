## Embedeed Schemas

Embedded schema memungkinkan kamu untuk mendefinisikan dan memvalidasi struktur data. Data ini akan tinggal di memory, atau dapat disimpan di database. Beberapa use-case untuk embedded schemas menyertakan:

- Kamu memaintaince data tinggal menengah, seperti ketika UI form fields memetakan beberapa table di dalam database.
- Kamu bekerja di dalam sebuah schema parent persisted dan kamu mau mengembed data yang
  - simple, seperti map dari user preference di dalam User schema
  - sering mengubah, seperti sebuah list product images dengan data struktur yang diasosiasikan di dalam schema Product
  - memerlukan tracking dan validation yang komplek, seperti sebuah schema Address di dalam User schema
- Kamu menggunakan sebuah document storage database dan ingin berinteraksi dan menipulasi embedded document.

**User Profile Example**

Mari kita explore sebuh contoh di mana kita mempunyai sebuah User dan ingin menyimpan informasi "profile" tentang mereka. Data yang kita ingin simpan adalah informasi UI-dependent yang sering berubah seiring waktu bersama perubahan di dalam UI. Juga, data ini belum cukup penting untuk menjamin User field di dalam User schema, karena bukan data yang mendasar bagi user. Sebuah embedded schema adalah solusi yang bagus untuk data semacam ini.

```elixir
defmodule User do
  use Ecto.Schema

  schema "users" do
    field :full_name, :string
    field :email, :string
    field :avatar_url, :string
    field :confirmed_at, :naive_datetime

    embeds_one :profile, Profile do
      field :online, :boolean
      field :dark_mode, :boolean
      field :visibility, Ecto.Enum, values: [:public, :private, :friends_only]
    end

    timestamps()
  end
end

```

**Embeds**

Ada 2 cara untuk merepresentasi embedded data di dalam schema, `Ecto.Schema.embeds_many/3`, yang membuat sebuah list embeds, dan `Ecto.Schema.embeds_one/3`, yang membuat hanya 1 instance dari embed. Pilihanmu di sini mempengaruhi perilaku dari embed-specific function seperti `Ecto.Changeset.put_embed/4` dan `Ecto.Changeset.cast_embed/3`, Jadi pilih apapun yang paling cocok untuk use-casemu. Di contoh yang akan kita gunakan `Ecto.Schema.embeds_one/3` karena user hanya akan memiliki 1 profil yang diasosiasikan dengannya:

```elixir
defmodule User do
  use Ecto.Schema

  schema "users" do
    field :full_name, :string
    field :email, :string
    field :avatar_url, :string
    field :confirmed_at, :naive_datetime

    embeds_one :profile, Profile do
      field :online, :boolean
      field :dark_mode, :boolean
      field :visibility, Ecto.Enum, values: [:public, :private, :friends_only]
    end

    timestamps()
  end
end

```

Embedded schemas didefinisikan dengan cara seperti itu dikatakan didefinisikan sebaris, yang memang demikian:

- Digenerate seperti sebuah module di parent scope dengan struct yang cocok (untuk contoh di atas, module adalah `User.Profile`)
- Disimpan di dalam parent schema
- Diperlukan untuk menyediakan `with` options ke `Ecto.Changeset.cast_embed/3`

**Extracting the embeds**

Karena schema `User` di atas sederhana dan cukup, kita mungkin ingin bekerja secara independen dengan struct profile embed. Untuk contoh, jika ada banyak functionality memutuskan untuk memanipulasi profile data sendiri, kita dapat mempertimbangkan untuk embedded schema ke modulenya sendiri. Ini dapat dicapai dengan `Ecto.Schema.embedded_schema/1`.

```elixir
# user/user.ex
defmodule User do
  use Ecto.Schema

  schema "users" do
    field(:full_name, :string)
    field(:email, :string)
    field(:avatar_url, :string)
    field(:confirmed_at, :naive_datetime)

    # embeds_one :profile, Profile do
    #   field(:online, :boolean)
    #   field(:dark_mode, :boolean)
    #   field(:visibility, Ecto.Enum, values: [:public, :private, :friends_only])
    # end
    # kita refactor dengan memanggil user_profile.ex

    embeds_one :profile, UserProfile

    timestamps()
  end
end
# user/user_profile.ex
defmodule UserProfile do
  use Ecto.Schema

  embedded_schema do
    field(:online, :boolean)
    field(:dark_mode, :boolean)
    field(:visibility, Ecto.Enum, values: [:public, :private, :friends_only])
  end
end

```

Embedded schema didefinisikan dengan cara itu dikatakan explisit-defined, yang:

- module khusus (dedicated) yang mempunya scope sendiri, changeset function, props, documentation, dll
- dapat diembed oleh beberapa parent schema
- persistence agnostic, yang berarti bahwa `embedded_schema` tidak perlu disimpan (persisted)

Penting diingat bahwa `embedded_schema` mempunya banyak use-case independed dari `embeds_one` dan `embeds_many`. Karena mereka persistent agnostic, mereka adalah skenario yang ideal di mana kamu ingin untuk memanage data struktur tanpa perlu menyimpannya. Untuk contoh, jika kamu ingin membangun form contact, kamu masih harus parse dan memvalidasi data, tapi data sepertinya tidak disimpan di manapun. Malahan, data itu digunakan untuk mengirim sebuah email. Embedded schema sangat cocok untuk use-case seperti itu.

**Migrations**

Jika kamu ingin menyimpan embedded schema ke database, kamu perlu menulis migration untuk menyertakan embedded data. 

```elixir
alter table("users") do
  add :profile, :map
end
```

Mau kamu menggunakan `embeds_one` atau `embeds_many`, sangat direkomendasikan untuk menggunakan `:map` data type (walaupun `{:array, :map}` akan bekerja dengan baik dengan `embeds_many`). Alasannya adalah pada umumnya relational database sepertinya merepresentasikan `:map` sebagai JSON (atau JSONB di Postgres), memungkinkan library Ecto adapter lebih ke flexibility dibanding bagaimana secara efisien menyimpan data.

**Changeset**

Changeset functionality untuk embeds akan mengijinkan kamu untuk mendorong validasi di data. Kamu dapat mendefinisikan sebuah changeset function untuk masing-masing modul. Untuk contoh, UserProfile module dapat "required" `online` dan `visibility` field untuk ada ketika men-generate sebuah changeset.

```elixir
defmodule UserProfile do
  # ...

  def changeset(%UserProfile{} = profile, attrs \\ %{}) do
    profile
    |> cast(attrs, [:online, :dark_mode, :visibility])
    |> validate_required([:online, :visibility])
  end
end

profile = %UserProfile{}
UserProfile.changeset(profile, %{online: true, visibility: :public})
```

Sedangkan, User changeset functon dapat require validasinya sendiri tanpa khawatir tentang detail dari perubahan UserProfile karena itu dapat mengoper tanggungjawab ke UserProfile via `cast_embed/3`. Sebuah validasi yang gagal di sebuah embed akan menyebabkan parent changeset menjadi invalid, bahkan ketika parent changeset sendiri tidak ada errors.

```elixir
defmodule User do
  # ...

  def changeset(user, attrs \\ %{}) do
    user
    |> cast(attrs, [:full_name, :email, :avatar_url])
    |> cast_embed(:profile, required: true)
  end
end

changeset = User.changeset(%User{}, %{profile: %{online: true}})
changeset.valid? # => false; "visibility can't be blank"
changeset = User.changeset(%User{}, %{profile: %{online: true, visibility: :public}})
changeset.valid? # => true
```

Di situasi di mana kamu harus menjaga embedded schema di dalam parent module, kamutidak harus mengextract UserProfile, kamu tetap harus mengcustom function untuk embedded data di dalam parent schema.

```elixir
defmodule User do
  use Ecto.Schema

  schema "users" do
    field :full_name, :string
    field :email, :string
    field :avatar_url, :string
    field :confirmed_at, :naive_datetime

    embeds_one :profile, Profile do
      field :online, :boolean
      field :dark_mode, :boolean
      field :visibility, Ecto.Enum, values: [:public, :private, :friends_only]
    end

    timestamps()
  end

  def changeset(%User{} = user, attrs \\ %{}) do
    user
    |> cast(attrs, [:full_name, :email])
    |> cast_embed(:profile, required: true, with: &profile_changeset/2)
  end

  def profile_changeset(profile, attrs \\ %{}) do
    profile
    |> cast(attrs, [:online, :dark_mode, :visibility])
    |> validate_required([:online, :visibility])
  end
end

changeset = User.changeset(%User{}, %{profile: %{online: true, visibility: :public}})
changeset.valid? # => true
```

**Querying embedded data**

Sekali kamu telah menulis embedded data ke database, kamu dapat menggunakannya di query pada parent schema:

```elixir
user_changeset = User.changeset(%User{}, %{profile: %{online: true, visibility: :public}})
{:ok, _user} = Repo.insert(user_changeset)

(Ecto.Query.from u in User, select: {u.profile["online"], u.profile["visibility"]}) |> Repo.one
# => {true, "public"}

(Ecto.Query.from u in User, select: u.profile, where: u.profile["visibility"] == ^:public) |> Repo.all
# => [
#  %UserProfile{
#    id: "...",
#    online: true,
#    dark_mode: nil,
#    visibility: :public
#  }
#]
```

Di dalam database dimana `:map` disimpan sebagai JSONB (like Postgres), Ecto membangun jsonpath query untukmu. Lebih banyak contoh embedded schema query ada di [json_extract_path/2](https://hexdocs.pm/ecto/Ecto.Query.API.html#json_extract_path/2)