## Testing with Ecto

Setelah kita sukses mensetup koneksi database dengan Ecto untuk aplikasi, koneksi tersebut akan digunakan untuk testmu kalau ada perubahan kedepannya. Khususnya jika kamu ingin memanfaatkan Ecto SQL Sandbox yang memungkinkanmu untuk menjalankan test dengan berbicara ke database secara bersamaan.

Buat `config/test.exs` dan buat kode seperti berikut:

```elixir
import Config

config :my_app, MyApp.Repo,
  username: "postgres",
  password: "postgres",
  database: "myapp_test",
  hostname: "localhost",
  pool: Ecto.Adapters.SQL.Sandbox

```

Kita telah mengkonfigurasi koneksi database untuk test setup kita. Di kasus ini, kita menggunakan Postgres database dan mengaturnya untuk menggunakan sandbox pool yang akan membungkus masing-masing test di transaction.

Pastikan kita telah mengimport konfigurasi untuk test environtment di posisi paling bawah `config/config.exs`:

```elixir
import_config "#{config_env()}.exs"
```

Kita juga perlu untuk menambahkan sebuah statement explisit ke baris terakhir di `test/test_helper.exs` tentang `sandbox` mode:

```elixir
Ecto.Adapters.SQL.Sandbox.mode(MyApp.Repo, :manual)
```

Terakhir, kita harus menjalankan koneksi database ke test. Kita dapat mengenablenya untuk semua test cases dengan mengextend `ExUnit` template atau dengan mengatur secara individu pada masing-masing test. Mari mulai dengan former dan tempatkan di `test/support/repo_case.ex`:

```elixir
defmodule MyApp.RepoCase do
  use ExUnit.CaseTemplate

  using do
    quote do
      alias MyApp.Repo

      import Ecto
      import Ecto.Query
      import MyApp.RepoCase

      # and any other stuff
    end
  end

  setup tags do
    :ok = Ecto.Adapters.SQL.Sandbox.checkout(MyApp.Repo)

    unless tags[:async] do
      Ecto.Adapters.SQL.Sandbox.mode(MyApp.Repo, {:shared, self()})
    end

    :ok
  end
end
```

case template di atas membawa `Ecto` dan `Ecto.Query` function ke testmu dan checks-out database connection. Case template itu juga mengenable sebuah shared sandbox connection mode semisal test tidak berjalan asynchronously. Buka [Ecto.Adapters.SQL.Sandbox](https://hexdocs.pm/ecto_sql/Ecto.Adapters.SQL.Sandbox.html) untuk info lebih lanjut.

Untuk menambahkan `test/support` folder untuk kompilasi di test environment kita perlu mengupdate konfigurasi `mix.exs`:

```elixir
  def project do
    [
      # ...
      elixirc_paths: elixirc_paths(Mix.env())
    ]
  end

  # Specifies which paths to compile per environment.
  defp elixirc_paths(:test), do: ["lib", "test/support"]
  defp elixirc_paths(_), do: ["lib"]
```

Dan kemudian di masing-masing test yang menggunakan repository:

```elixir
defmodule MyApp.MyTest do
  use MyApp.RepoCase

  # Tests etc...
end
```

Semisal tidak ingin mendefinisikan sebuah "case template", kamu dapat checkout masing-masing individual case:

```elixir
defmodule MyApp.MyTest do
  use ExUnit.Case

  setup do
    :ok = Ecto.Adapters.SQL.Sandbox.checkout(MyApp.Repo)
  end

  # Tests etc...
end
```

Untuk alasan kenyamanan, kamu dapat mendefinisikan `aliases` untuk secara otomatis mengsetup databasemu pada saat eksekusi tests. Ubah isi dari `mix.exs`:

```elixir

  def project do
    [app: :my_app,

     ...

     aliases: aliases()]
  end

  defp aliases do
    [ ...
     "test": ["ecto.create --quiet", "ecto.migrate", "test"]
    ]
  end
```